# - artConfig Template file

# - May not need this variable as versioning file
# holds details, but seems to be convention
set(@PROJECT_NAME@_VERSION "@PROJECT_VERSION@")

# - CMakePackageConfigHelpers Startup...
@PACKAGE_INIT@

# Don't *strictly* need include dir(s) as linking to targets
# will propagate all needed requirements. However, it's
# useful if we're using header-only parts and don't want to
# have a binary linkage.
# NB: Could also/Should be derived from INTERFACE_INCLUDE_DIRS
# as this will add paths from deps (e.g Boost). However,
# as that would involve a genexp its propagation needs thought.
set_and_check(@PROJECT_NAME@_INCLUDE_DIR "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@")
set(@PROJECT_NAME@_INCLUDE_DIRS "${@PROJECT_NAME@_INCLUDE_DIR}")

# - When needed, refind needed targets here
# - Assert EXACT version as this is default behaviour of UPS setups.
find_package(canvas @canvas_VERSION@ EXACT REQUIRED)
find_package(messagefacility @messagefacility_VERSION@ EXACT REQUIRED)
find_package(fhiclcpp @fhiclcpp_VERSION@ EXACT REQUIRED)
find_package(cetlib @cetlib_VERSION@ EXACT REQUIRED)
find_package(TBB @TBB_VERSION_MAJOR@.@TBB_VERSION_MINOR@ EXACT REQUIRED)
find_package(CLHEP @CLHEP_VERSION@ EXACT REQUIRED)
find_package(ROOT @ROOT_VERSION@ EXACT REQUIRED)
find_package(Boost @Boost_MAJOR_VERSION@.@Boost_MINOR_VERSION@ EXACT REQUIRED filesystem system thread)

# NB: Assumes we have FindSQLite or SQLiteConfig available
find_package(SQLite @SQLite_VERSION@ EXACT REQUIRED)


# - Include our targets
include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")

# - Make modules for provided tools available to clients
list(INSERT CMAKE_MODULE_PATH 0 "${CMAKE_CURRENT_LIST_DIR}")

